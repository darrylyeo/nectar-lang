WHITESPACE = _{ (" " | "\n" | NEWLINE)+ }
alpha = @{ 'a'..'z' | 'A'..'Z' }
digit = @{ '0'..'9' }

identifier = @{ alpha ~ (alpha | digit | "_" | "-")* }

string_content = { (!PEEK ~ ANY)* }
string = { (PUSH("\""+) | PUSH("'"+)) ~ string_content ~ POP }

integer = { digit+ }
decimal = @{ digit* ~ "." ~ digit+ }
number = @{ (decimal | integer) ~ (^"e" ~ integer)? }

unit = { non_keyword ~ ("/" ~ non_keyword)* }
quantity = { number ~ unit }

article = _{ ^"an" | ^"a" | ^"the" }
and = _{ ^"and" }
or = _{ ^"or" }
is = _{ (^"is" | ^"are" ~ ^"all"? | ^"can be" | ^"am") }
has = _{ (^"has" | ^"have") ~ article? }
of = _{ ^"of" }
with = _{ ^"with" }
aka = _{ ^"aka" | ^"alias" | (^"also" | ^"better" | ^"commonly" | ^"otherwise" | ^"widely")? ~ (^"called" | ^"known as" | ^"named" | ^"titled") | or }

keyword = _{ article | and | or | is | has | of | with | aka }
non_keyword = _{ !(keyword) ~ identifier }

expression = { string | quantity | number }

noun = @{ article? ~ "@" ~ identifier | "@" ~ string }
noun_disjunction = _{ noun ~ ("," ~ noun)* ~ (","? ~ or ~ noun)+ }
noun_conjunction = _{ noun ~ ("," ~ noun)* ~ (","? ~ and ~ noun)+ }
noun_alias = _{
	"(" ~ aka? ~ (noun | noun_disjunction) ~ ")" |
	","? ~ aka ~ (noun | noun_disjunction) ~ ","?
}

subject = {
	noun ~ noun_alias?
}
subjects = {
	subject ~ ("," ~ subject)* ~ (","? ~ and ~ subject)+ |
	subject ~ (";" ~ subject)* ~ (";"? ~ and ~ subject)+ |
	subject ~ ("/" ~ subject)*
}

category = @{ article? ~ "#" ~ identifier }
category_disjunction = { category ~ ("," ~ category)* ~ (","? ~ or ~ category)+ }
category_conjunction = { category ~ ("," ~ category)* ~ (","? ~ and ~ category)+ }
category_alias = _{
	"(" ~ aka? ~ (category | category_disjunction) ~ ")" |
	","? ~ aka ~ (category | category_disjunction) ~ ","?
}

categorization = {
	category ~ category_alias?
}
categorizations = {
	categorization ~ ("," ~ categorization)* ~ (","? ~ and ~ categorization)+ |
	categorization ~ (";" ~ categorization)* ~ (";"? ~ and ~ categorization)+ |
	categorization ~ ("/" ~ categorization)*
}

property = { identifier }

relation = {
	^"to"? ~ (
		is |
    	has |
    	^"created by" |
    	^"represent" |
    	^"works with" |
    	^"compiles to" |
		^"says"
    )
}


is_a_predicate = { ( is ~ article? | has ~ article? ~ (^"category" | ^"type") ~ of? ) ~ categorizations }
has_property_predicate = { (has | with) ~ article? ~ property ~ of? ~ expression }
relation_predicate = { relation ~ subject }
hyper_relation_predicate = { relation ~ categorizations }

predicate = { is_a_predicate | has_property_predicate | relation_predicate | hyper_relation_predicate }
predicates = { predicate ~ (("," | and)? ~ predicate)* }


compound_statement = { subjects ~ predicates }
statements = { compound_statement ~ (identifier* ~ "." ~ compound_statement)* }