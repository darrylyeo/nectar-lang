WHITESPACE = _{ (" " | "\n" | NEWLINE)+ }
alpha = @{ 'a'..'z' | 'A'..'Z' }
digit = @{ '0'..'9' }

identifier = @{ alpha ~ (alpha | digit | "_" | "-")* }

string_content = { (!PEEK ~ ANY)* }
string = { (PUSH("\""+) | PUSH("'"+)) ~ string_content ~ POP }

integer = { digit+ }
decimal = @{ digit* ~ "." ~ digit+ }
number = @{ (decimal | integer) ~ (^"e" ~ integer)? }

unit = { non_keyword ~ ("/" ~ non_keyword)* }
quantity = { number ~ unit }

article = _{ ^"an" | ^"a" | ^"the" }
and = _{ ^"and" }
or = _{ ^"or" }
is = _{ (^"is" | ^"are" ~ ^"all"? | ^"can be" | ^"am") }
has = _{ ^"has" | ^"have" }
of = _{ ^"of" }
with = _{ ^"with" }
aka = _{ ^"aka" | ^"alias" | (^"also" | ^"better" | ^"commonly" | ^"otherwise" | ^"widely")? ~ (^"called" | ^"known as" | ^"named" | ^"titled") | or }

keyword = _{ article | and | or | is | has | of | with | aka }
non_keyword = _{ !(keyword) ~ identifier }

property_expression = { string | quantity | number }

noun = @{(article ~ WHITESPACE)? ~ "@" ~ identifier | "@" ~ string }
noun_disjunction = _{ noun ~ ("," ~ noun)* ~ (","? ~ or ~ noun)+ }
noun_conjunction = _{ noun ~ ("," ~ noun)* ~ (","? ~ and ~ noun)+ }
noun_alias = _{
	"(" ~ aka? ~ (noun | noun_disjunction) ~ ")" |
	","? ~ aka ~ (noun | noun_disjunction) ~ ","?
}

noun_entity = {
	noun ~ noun_alias?
}
nouns = {
	noun_entity ~ ("," ~ noun_entity)* ~ (","? ~ and ~ noun_entity)+ |
	noun_entity ~ (";" ~ noun_entity)* ~ (";"? ~ and ~ noun_entity)+ |
	noun_entity ~ ("/" ~ noun_entity)*
}
subjects = _{ nouns }
objects = _{ nouns }

category = @{ (article ~ WHITESPACE)? ~ "#" ~ identifier }
category_disjunction = { category ~ ("," ~ category)* ~ (","? ~ or ~ category)+ }
category_conjunction = { category ~ ("," ~ category)* ~ (","? ~ and ~ category)+ }
category_alias = _{
	"(" ~ aka? ~ (category | category_disjunction) ~ ")" |
	","? ~ aka ~ (category | category_disjunction) ~ ","?
}

category_entity = {
	category ~ category_alias?
}
categories = {
	category_entity ~ ("," ~ category_entity)* ~ (","? ~ and ~ category_entity)+ |
	category_entity ~ (";" ~ category_entity)* ~ (";"? ~ and ~ category_entity)+ |
	category_entity ~ ("/" ~ category_entity)*
}

property = { article? ~ identifier }

relation = {
	^"to"? ~ (
		is |
    	has |
    	^"created by" |
    	^"represent" |
    	^"works with" |
    	^"compiles to" |
		^"says" |
		^"loves" |
		^"likes" |
		^"made of" |
		^"works for" |
		^"made of" |
		^"made of" |
		^"made of"
    )
}


categorization_predicate = { (is | has ~ (^"category" | ^"type") ~ of?) ~ categories }
has_property_predicate = { (has | with) ~ property ~ of? ~ property_expression }
relation_predicate = { relation ~ objects }
hyper_relation_predicate = { relation ~ categories }

predicate = { categorization_predicate | has_property_predicate | relation_predicate | hyper_relation_predicate }
predicates = { predicate ~ (("," | and)? ~ predicate)* }


compound_statement = { subjects ~ predicates }
statements = { compound_statement ~ (identifier* ~ "." ~ compound_statement)* }